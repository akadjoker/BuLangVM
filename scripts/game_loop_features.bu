import math;
import raylib;
using raylib;

class Bunny
{
    var x, y, vx, vy, life;

    def init(x, y)
    {
        self.x = x;
        self.y = y;
        self.vx = (math.rand(200) - 100) / 12.0;
        self.vy = (math.rand(200) - 100) / 12.0;
        self.life = 600 + math.rand(600);
    }

    def update(w, h)
    {
        if (self.life <= 0) return false;

        self.x += self.vx;
        self.y += self.vy;
        self.vy += 0.5;

        if (self.y > h)
        {
            self.y = h;
            self.vy = self.vy * -0.85;
        }

        if (self.x < 0 || self.x > w)
        {
            self.vx = self.vx * -1.0;
        }

        self.life -= 1;
        return self.life > 0;
    }

    def draw(tex, color)
    {
        DrawTexture(tex, self.x, self.y, color);
    }
}

def clamp(v, min, max)
{
    if (v < min) return min;
    if (v > max) return max;
    return v;
}

def spawnBunnies(list, x, y, count)
{
    if (count <= 0) return 0;
    var added = 0;
    var i = 0;
    while (i < count)
    {
        list.push(Bunny(x, y));
        added++;
        i++;
    }
    return added;
}

def removeSome(list, count)
{
    if (count <= 0) return 0;
    var removed = 0;
    while (count > 0 && len(list) > 0)
    {
        list.remove(len(list) - 1);
        removed++;
        count--;
    }
    return removed;
}

def firstFastIndex(list, threshold)
{
    var i = 0;
    while (i < len(list))
    {
        var b = list[i];
        if (b.vx > threshold || b.vx < -threshold || b.vy > threshold || b.vy < -threshold)
        {
            return i;
        }
        i++;
    }
    return -1;
}

InitWindow(800, 450, "BuLang Game Loop Features");

var BLACK = Color(0, 0, 0, 255);
var WHITE = Color(255, 255, 255, 255);
var RED = Color(255, 0, 0, 255);
var LIGHTGRAY = Color(200, 200, 200, 255);

var tex = LoadTexture("assets/wabbit_alpha.png");
SetTargetFPS(60);

var bunnies = [];
var autoSpawn = true;
var autoRemove = true;
var totalAdded = 0;
var totalRemoved = 0;

while (!WindowShouldClose())
{
    if (IsKeyPressed(KEY_SPACE)) autoSpawn = !autoSpawn;
    if (IsKeyPressed(KEY_BACKSPACE)) autoRemove = !autoRemove;

    if (IsMouseButtonDown(0))
    {
        var mx = clamp(GetMouseX(), 0, 800);
        var my = clamp(GetMouseY(), 0, 400);
        totalAdded += spawnBunnies(bunnies, mx, my, 200);
    }

    if (IsMouseButtonDown(1))
    {
        totalRemoved += removeSome(bunnies, 200);
    }

    if (autoSpawn)
    {
        var mx2 = clamp(GetMouseX(), 0, 800);
        var my2 = clamp(GetMouseY(), 0, 400);
        totalAdded += spawnBunnies(bunnies, mx2, my2, 20);
    }

    if (autoRemove)
    {
        totalRemoved += removeSome(bunnies, 5);
    }

    BeginDrawing();
    ClearBackground(BLACK);

    var alive = [];
    var i = 0;
    while (i < len(bunnies))
    {
        var b = bunnies[i];
        if (b.update(800, 400))
        {
            b.draw(tex, WHITE);
            alive.push(b);
        }
        else
        {
            totalRemoved++;
        }
        i++;
    }
    bunnies = alive;

    var fastIdx = firstFastIndex(bunnies, 5.0);

    DrawText(format("count {}", len(bunnies)), 10, 30, 20, LIGHTGRAY);
    DrawText(format("added {}", totalAdded), 10, 55, 20, LIGHTGRAY);
    DrawText(format("removed {}", totalRemoved), 10, 80, 20, LIGHTGRAY);
    DrawText(format("autoSpawn {} | autoRemove {}", autoSpawn, autoRemove), 10, 105, 20, LIGHTGRAY);

    if (fastIdx >= 0)
    {
        DrawText(format("fast idx {}", fastIdx), 10, 130, 20, RED);
    }

    DrawFPS(10, 10);
    EndDrawing();
}

UnloadTexture(tex);
CloseWindow();
