require "raylib";
import math;
import raylib;
using raylib;

InitWindow(800, 600, "Metaballs");
SetTargetFPS(60);

var BLACK = Color(0, 0, 0, 255);
var WHITE = Color(255, 255, 255, 255);
var WIDTH = 160;
var HEIGHT = 120;
var PIXEL_SIZE = 5;

class Ball
{
    var x, y, vx, vy;
    var radius;
    
    def init()
    {
        self.x = math.rand(WIDTH);
        self.y = math.rand(HEIGHT);
        self.vx = (math.rand(40) - 20) / 10.0;
        self.vy = (math.rand(40) - 20) / 10.0;
        self.radius = 20 + math.rand(30);
    }
    
    def update()
    {
        self.x = self.x + self.vx;
        self.y = self.y + self.vy;
        
        if (self.x < 0 || self.x > WIDTH) self.vx = -self.vx;
        if (self.y < 0 || self.y > HEIGHT) self.vy = -self.vy;
    }
}

var balls = [];
for(var i=0; i<8; i=i+1)
{
    balls.push(Ball());
}

while (!WindowShouldClose())
{
    for(var i=0; i<len(balls); i=i+1)
    {
        balls[i].update();
    }
    
    BeginDrawing();
    ClearBackground(BLACK);
    
    // Calcular metaball field
    for(var y=0; y<HEIGHT; y=y+1)
    {
        for(var x=0; x<WIDTH; x=x+1)
        {
            var sum = 0.0;
            
            for(var i=0; i<len(balls); i=i+1)
            {
                var b = balls[i];
                var dx = b.x - x;
                var dy = b.y - y;
                var dist = sqrt(dx*dx + dy*dy);
                
                if (dist > 0.1)
                {
                    sum = sum + (b.radius * b.radius) / (dist * dist);
                }
            }
            
            if (sum > 1.0)
            {
                var intensity = sum * 100;
                if (intensity > 255) intensity = 255;
                
                var color = Color(intensity, intensity / 2, 255, 255);
                DrawRectangle(x * PIXEL_SIZE, y * PIXEL_SIZE, PIXEL_SIZE, PIXEL_SIZE, color);
            }
        }
    }
    
    DrawText("METABALLS", 10, 10, 20, WHITE);
    DrawFPS(10, 30);
    
    EndDrawing();
}

CloseWindow();