require "raylib";
import math;
import raylib;
using raylib;

InitWindow(800, 600, "Particles");
SetTargetFPS(60);

var BLACK = Color(0, 0, 0, 10);
var WHITE = Color(255, 255, 255, 255);

class Particle
{
    var x, y, vx, vy;
    var life;
    var color;
    
    def init(x, y)
    {
        self.x = x;
        self.y = y;
        
        var angle = (math.rand(360) / 360.0) * 6.28;
        var speed = math.rand(10) + 2;
        
        self.vx = cos(angle) * speed;
        self.vy = sin(angle) * speed;
        self.life = 255;
        
        var r = math.rand(3);
        if (r == 0)
        {
            self.color = Color(255, 100, 100, 255);
        }
        else if (r == 1)
        {
            self.color = Color(100, 255, 100, 255);
        }
        else
        {
            self.color = Color(100, 100, 255, 255);
        }
    }
    
    def update()
    {
        self.x = self.x + self.vx;
        self.y = self.y + self.vy;
        self.vy = self.vy + 0.2;  // Gravidade
        self.life = self.life - 2;
    }
    
    def draw()
    {
        if (self.life > 0)
        {
            var c = Color(self.color.r, self.color.g, self.color.b, self.life);
            DrawCircle(self.x, self.y, 3, c);
        }
    }
    
    def isDead()
    {
        return self.life <= 0;
    }
}

var particles = [];

while (!WindowShouldClose())
{
    // Click para explodir
    if (IsMouseButtonPressed(0))
    {
        var mx = GetMouseX();
        var my = GetMouseY();
        
        for(var i=0; i<100; i=i+1)
        {
            particles.push(Particle(mx, my));
        }
    }
    
    // Update
    var alive = [];
    for(var i=0; i<len(particles); i=i+1)
    {
        particles[i].update();
        if (!particles[i].isDead())
        {
            alive.push(particles[i]);
        }
    }
    particles = alive;
    
    BeginDrawing();
    ClearBackground(BLACK);
    
    for(var i=0; i<len(particles); i=i+1)
    {
        particles[i].draw();
    }
    
    DrawText("Clica para explodir!", 10, 10, 20, WHITE);
    DrawText(format("Particles: {}", len(particles)), 10, 35, 16, WHITE);
    DrawFPS(700, 10);
    
    EndDrawing();
}

CloseWindow();