require "raylib";
import math;
import raylib;
using raylib;

InitWindow(800, 600, "Mandelbrot");
SetTargetFPS(60);

var BLACK = Color(0, 0, 0, 255);
var WHITE = Color(255, 255, 255, 255);

var zoom = 1.0;
var offsetX = 0.0;
var offsetY = 0.0;
var maxIter = 50;

def mandelbrot(cx, cy)
{
    var x = 0.0;
    var y = 0.0;
    var iter = 0;
    
    while (x*x + y*y < 4 && iter < maxIter)
    {
        var xtemp = x*x - y*y + cx;
        y = 2*x*y + cy;
        x = xtemp;
        iter = iter + 1;
    }
    
    return iter;
}

var needsRedraw = true;

while (!WindowShouldClose())
{
    // Controles
    if (IsKeyDown(265)) offsetY = offsetY + 0.1 / zoom;  // UP
    if (IsKeyDown(264)) offsetY = offsetY - 0.1 / zoom;  // DOWN
    if (IsKeyDown(263)) offsetX = offsetX + 0.1 / zoom;  // LEFT
    if (IsKeyDown(262)) offsetX = offsetX - 0.1 / zoom;  // RIGHT
    if (IsKeyPressed(87)) { zoom = zoom * 1.5; needsRedraw = true; }  // W
    if (IsKeyPressed(83)) { zoom = zoom / 1.5; needsRedraw = true; }  // S
    if (IsKeyPressed(69)) { maxIter = maxIter + 10; needsRedraw = true; }  // E
    if (IsKeyPressed(81) && maxIter > 10) { maxIter = maxIter - 10; needsRedraw = true; }  // Q
    
    BeginDrawing();
    
    if (needsRedraw)
    {
        ClearBackground(BLACK);
        
        var step = 4;  // Desenha cada 4 pixels para ser mais r√°pido
        
        for(var px=0; px<800; px=px+step)
        {
            for(var py=0; py<600; py=py+step)
            {
                var x = (px - 400) / (200.0 * zoom) + offsetX;
                var y = (py - 300) / (200.0 * zoom) + offsetY;
                
                var iter = mandelbrot(x, y);
                
                var brightness = (iter * 255) / maxIter;
                var color = Color(brightness, brightness / 2, 255 - brightness, 255);
                
                DrawRectangle(px, py, step, step, color);
            }
        }
        
        needsRedraw = false;
    }
    
    DrawText("WASD: Move | W/S: Zoom | Q/E: Detail", 10, 10, 16, WHITE);
    DrawText(format("Zoom: {} | Iter: {}", zoom, maxIter), 10, 30, 14, WHITE);
    DrawFPS(700, 10);
    
    EndDrawing();
}

CloseWindow();