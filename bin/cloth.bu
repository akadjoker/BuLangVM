require "raylib";
import math;
import raylib;
using raylib;

InitWindow(800, 600, "BuLang STRESS TEST");
SetTargetFPS(60);

var GRAVITY = 0.8;
var BOUNCE = 0.3;
var FRICTION = 0.98;
var SPACING = 15;  // Mais pequeno = mais denso

var GREEN = Color(0, 255, 0, 255);
var WHITE = Color(255, 255, 255, 255);
var RED = Color(255, 0, 0, 255);
var BLACK = Color(0, 0, 0, 255);
var BLUE = Color(100, 150, 255, 255);

class Point
{
    var x, y, oldx, oldy;
    var pinned;

    def init(x, y, pinned)
    {
        self.x = x; 
        self.y = y;
        self.oldx = x; 
        self.oldy = y;
        self.pinned = pinned;
    }

    def update()
    {
        if (!self.pinned) 
        {
            var vx = (self.x - self.oldx) * FRICTION;
            var vy = (self.y - self.oldy) * FRICTION;

            self.oldx = self.x;
            self.oldy = self.y;

            self.x = self.x + vx;
            self.y = self.y + vy + GRAVITY;

            if (self.y > 590)
            {
                self.y = 590;
                self.oldy = self.y + vy * BOUNCE;
            }
            if (self.x < 10)
            {
                self.x = 10;
                self.oldx = self.x - vx * BOUNCE;
            }
            if (self.x > 790)
            {
                self.x = 790;
                self.oldx = self.x - vx * BOUNCE;
            }
        }
    }
    
    def drag(mx, my)
    {
        var dx = self.x - mx;
        var dy = self.y - my;
        if (dx*dx + dy*dy < 900 && IsMouseButtonDown(0))
        {
            self.x = mx;
            self.y = my;
            self.oldx = self.x;
            self.oldy = self.y;
        }
        if (dx*dx + dy*dy < 400 && IsMouseButtonDown(1))
        {
            self.pinned = false;
        }
    }
}

class Stick
{
    var p1, p2, length;
    
    def init(p1, p2)
    {
        self.p1 = p1;
        self.p2 = p2;
        var dx = p1.x - p2.x;
        var dy = p1.y - p2.y;
        self.length = sqrt(dx*dx + dy*dy);
    }

    def update()
    {
        var dx = self.p2.x - self.p1.x;
        var dy = self.p2.y - self.p1.y;
        var dist = sqrt(dx*dx + dy*dy);
        
        if (dist > 0.01)
        {
            var diff = self.length - dist;
            var percent = (diff / dist) / 2;
            
            var offX = dx * percent;
            var offY = dy * percent;

            if (!self.p1.pinned)
            {
                self.p1.x = self.p1.x - offX;
                self.p1.y = self.p1.y - offY;
            }
            if (!self.p2.pinned)
            {
                self.p2.x = self.p2.x + offX;
                self.p2.y = self.p2.y + offY;
            }
        }
    }

    def draw()
    {
        DrawLine(self.p1.x, self.p1.y, self.p2.x, self.p2.y, WHITE);
    }
}

// AUMENTA ESTES VALORES ATÃ‰ CRASHAR! ðŸ’€
var COLS = 40;  // ComeÃ§a com 40
var ROWS = 30;  // ComeÃ§a com 30

var points = [];
var sticks = [];

DrawText("A criar tecido...", 300, 300, 20, WHITE);

// Criar pontos
for(var y=0; y<ROWS; y=y+1)
{
    for(var x=0; x<COLS; x=x+1)
    {
        var pinned = (y == 0) && (x % 5 == 0);
        var p = Point(100 + x * SPACING, 50 + y * SPACING, pinned);
        points.push(p);
    }
}

// Criar sticks
for(var y=0; y<ROWS; y=y+1)
{
    for(var x=0; x<COLS; x=x+1)
    {
        var idx = y * COLS + x;
        
        if (x < COLS - 1) 
        {
            sticks.push(Stick(points[idx], points[idx+1]));
        }
        
        if (y < ROWS - 1) 
        {
            sticks.push(Stick(points[idx], points[idx+COLS]));
        }
    }
}

var frameCount = 0;
var avgFPS = 60;

while (!WindowShouldClose())
{
    var mx = GetMouseX();
    var my = GetMouseY();
    
    frameCount = frameCount + 1;
    
    // Physics
    for(var i=0; i<len(points); i=i+1)
    {
        points[i].update();
        points[i].drag(mx, my);
    }
    
    // Constraints
    for(var iter=0; iter<3; iter=iter+1)
    {
        for(var i=0; i<len(sticks); i=i+1)
        {
            sticks[i].update();
        }
    }

    BeginDrawing();
    ClearBackground(BLACK);
    
    for(var i=0; i<len(sticks); i=i+1) 
    {
        sticks[i].draw();
    }
    
    for(var i=0; i<len(points); i=i+1) 
    {
        var color = BLUE;
        if (points[i].pinned)
        {
            color = RED;
        }
        DrawCircle(points[i].x, points[i].y, 2, color);
    }

    // Stats
    DrawText("STRESS TEST MODE", 10, 10, 20, RED);
    DrawText(format("Pontos: {}", len(points)), 10, 35, 16, GREEN);
    DrawText(format("Sticks: {}", len(sticks)), 10, 55, 16, GREEN);
    DrawText(format("Frame: {}", frameCount), 10, 75, 16, GREEN);
    DrawFPS(700, 10);
    
    EndDrawing();
}

CloseWindow();