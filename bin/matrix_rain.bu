require "raylib";
import math;
import raylib;
using raylib;

InitWindow(800, 600, "BuLang Matrix");
SetTargetFPS(60);

var FONT_SIZE = 20;
var COLUMNS = 800 / FONT_SIZE;
var GREEN = Color(0, 255, 0, 255);
var BRIGHT_GREEN = Color(180, 255, 180, 255);
var DARK_GREEN = Color(0, 80, 0, 255);
var BLACK = Color(0, 0, 0, 255);
var WHITE = Color(255, 255, 255, 255);

var MATRIX_CHARS = "ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$%&*+-=<>[]{}";

class Stream
{
    var x, y;
    var speed;
    var chars;
    var count;
    var first;

    def init(x)
    {
        self.x = x;
        self.y = math.rand(600) * -1;  // Começa em posições variadas acima do ecrã
        self.speed = 2 + math.rand(5);
        self.count = 8 + math.rand(25);
        self.first = math.rand(100) > 70;
        self.chars = [];
        
        for (var i = 0; i < self.count; i = i + 1)
        {
            self.chars.push(math.rand(MATRIX_CHARS.length()));
        }
    }

    def update()
    {
        self.y = self.y + self.speed;
        
        // Reset quando a stream sair completamente do ecrã
        if (self.y - (self.count * FONT_SIZE) > 600)
        {
            self.y = -100 - math.rand(500);  // Volta para cima com delay aleatório
            self.speed = 2 + math.rand(5);
            self.count = 8 + math.rand(25);
            self.first = math.rand(100) > 70;
            self.chars = [];
            for (var i = 0; i < self.count; i = i + 1)
            {
                self.chars.push(math.rand(MATRIX_CHARS.length()));
            }
        }
        
        // Glitch effect
        if (math.rand(100) < 10)
        {
            self.chars[math.rand(self.count)] = math.rand(MATRIX_CHARS.length());
        }
    }

    def draw()
    {
        for (var i = 0; i < self.count; i = i + 1)
        {
            var charY = self.y - (i * FONT_SIZE);
            
            if (charY > -FONT_SIZE && charY < 600)
            {
                var color = GREEN;
                
                if (i == 0 && self.first)
                {
                    color = WHITE;
                }
                else if (i < 3)
                {
                    color = BRIGHT_GREEN;
                }
                else if (i > self.count - 5)
                {
                    var fadeRatio = (self.count - i) / 5.0;
                    color = Color(0, 80 + (175 * fadeRatio), 0, 255);
                }
                
                var char = MATRIX_CHARS.at(self.chars[i]);
                DrawText(char, self.x, charY, FONT_SIZE, color);
            }
        }
    }
}

// Criar 2-3 streams por coluna para mais densidade
var streams = [];
for (var col = 0; col < COLUMNS; col = col + 1)
{
    var x = col * FONT_SIZE;
    
    // 2-3 streams por coluna com diferentes offsets
    var numStreams = 2 + math.rand(2);  // 2 ou 3 streams
    for (var s = 0; s < numStreams; s = s + 1)
    {
        streams.push(Stream(x));
    }
}

var fadeOverlay = Color(0, 0, 0, 25);

while (!WindowShouldClose())
{
    BeginDrawing();
    DrawRectangle(0, 0, 800, 600, fadeOverlay);
    
    for (var i = 0; i < len(streams); i = i + 1)
    {
        streams[i].update();
        streams[i].draw();
    }
    
    DrawFPS(10, 10);
    EndDrawing();
}

CloseWindow();