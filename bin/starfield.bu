require "raylib";
import math;
import raylib;
using raylib;

InitWindow(800, 600, "Starfield");
SetTargetFPS(60);

var WHITE = Color(255, 255, 255, 255);
var BLACK = Color(0, 0, 0, 255);
var BLUE = Color(100, 150, 255, 255);

class Star
{
    var x, y, z;
    var pz;
    
    def init()
    {
        self.x = math.rand(1600) - 800;
        self.y = math.rand(1200) - 600;
        self.z = math.rand(1600);
        self.pz = self.z;
    }
    
    def update(speed)
    {
        self.z = self.z - speed;
        
        if (self.z < 1)
        {
            self.z = 1600;
            self.x = math.rand(1600) - 800;
            self.y = math.rand(1200) - 600;
            self.pz = self.z;
        }
    }
    
    def draw()
    {
        var sx = (self.x / self.z) * 400 + 400;
        var sy = (self.y / self.z) * 400 + 300;
        
        var r = (1600 - self.z) / 1600 * 8;
        if (r < 1) r = 1;
        
        var alpha = (1600 - self.z) / 1600 * 255;
        
        var px = (self.x / self.pz) * 400 + 400;
        var py = (self.y / self.pz) * 400 + 300;
        
        DrawLine(px, py, sx, sy, Color(255, 255, 255, alpha));
        DrawCircle(sx, sy, r, Color(255, 255, 255, alpha));
        
        self.pz = self.z;
    }
}

var stars = [];
for(var i=0; i<500; i=i+1)
{
    stars.push(Star());
}

var speed = 10;

while (!WindowShouldClose())
{
    var mx = GetMouseX();
    speed = (mx / 800.0) * 40 + 5;
    
    for(var i=0; i<len(stars); i=i+1)
    {
        stars[i].update(speed);
    }
    
    BeginDrawing();
    ClearBackground(BLACK);
    
    for(var i=0; i<len(stars); i=i+1)
    {
        stars[i].draw();
    }
    
    DrawText("Move o rato para acelerar!", 10, 10, 20, WHITE);
    DrawText(format("Speed: {}", speed), 10, 35, 16, BLUE);
    DrawFPS(700, 10);
    
    EndDrawing();
}

CloseWindow();