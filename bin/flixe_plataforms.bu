
require "raylib";
import math;
import raylib;
using raylib;

include "flixel.bu";

InitWindow(800, 600, "BuFlixel - World System");
SetTargetFPS(60);

var WHITE = Color(255, 255, 255, 255);
var BLACK = Color(0, 0, 0, 255);
var GREEN = Color(100, 255, 100, 255);
var RED = Color(255, 100, 100, 255);
var BLUE = Color(100, 150, 255, 255);

// Create world
var world = World();
var emitter = Emitter(); // Particle system
// Create groups
var players = Group();
var enemies = Group();
var platforms = Group();
var collectibles = Group();

// Create player
var player = Sprite(100, 100, 32, 32);
player.color = GREEN;
player.maxVelocityX = 200.0;
player.maxVelocityY = 500.0;
player.dragX = 800.0;
players.add(player);

// Create platforms
var ground = Sprite(0, 550, 800, 50);
ground.immovable = true;
ground.color = Color(128, 128, 128, 255);
platforms.add(ground);

var plat1 = Sprite(200, 400, 150, 20);
plat1.immovable = true;
plat1.color = BLUE;
platforms.add(plat1);

var plat2 = Sprite(450, 300, 150, 20);
plat2.immovable = true;
plat2.color = BLUE;
platforms.add(plat2);

// Create moving platform
var movingPlat = Sprite(500, 450, 100, 15);
movingPlat.immovable = true;
movingPlat.velocityX = 100.0;
movingPlat.color = Color(255, 200, 100, 255);
platforms.add(movingPlat);

// Create enemies
for (var i = 0; i < 3; i = i + 1)
{
    var enemy = Sprite(250 + i * 150, 200, 28, 28);
    enemy.color = RED;
    enemy.velocityX = 50.0 + math.rand(50);
    enemy.elasticity = 1.0;
    enemies.add(enemy);
}

// Create collectibles
for (var i = 0; i < 5; i = i + 1)
{
    var coin = Sprite(100 + i * 120, 150 + math.rand(200), 16, 16);
    coin.color = Color(255, 255, 0, 255);
    coin.immovable = true;
    collectibles.add(coin);
}

var score = 0;

// Callback for collecting coins
def onCollectCoin(player, coin)
{
    coin.exists = false;
    score = score + 10;
}

// Callback for hitting enemy
def onHitEnemy(player, enemy)
{
    if (player.velocityY > 0 && player.y < enemy.y)
    {
        // Bounce on enemy
        player.velocityY = -250.0;
        enemy.exists = false;
        score = score + 50;
        emitter.explode(enemy.x + enemy.width/2, enemy.y + enemy.height/2, 10);
    }
    else
    {
        // Hit by enemy - reset
        player.x = 100;
        player.y = 100;
        player.velocityX = 0;
        player.velocityY = 0;
    }
}

while (!WindowShouldClose())
{
    // ============================================
    // INPUT
    // ============================================

    var elapsed = GetFrameTime();
    
    if (IsKeyDown(KEY_RIGHT) || IsKeyDown(KEY_D))
    {
        player.accelerationX = 600.0;
    }
    else if (IsKeyDown(KEY_LEFT) || IsKeyDown(KEY_A))
    {
        player.accelerationX = -600.0;
    }
    else
    {
        player.accelerationX = 0.0;
    }
    
    if (IsKeyPressed(KEY_SPACE) && (player.touching & DOWN) != 0)
    {
        player.velocityY = -600.0;
        emitter.explode(player.x + player.width/2, player.y + player.height/2, 10);
    }
    
    // ============================================
    // UPDATE
    // ============================================
    
    // Apply gravity to player and enemies
    player.accelerationY = world.gravity;
    
    for (var i = 0; i < len(enemies.members); i = i + 1)
    {
        enemies.members[i].accelerationY = world.gravity;
    }
    
    // Update groups
    players.update(elapsed);
    enemies.update(elapsed);
    platforms.update(elapsed);
    emitter.update(elapsed);
    
    // Moving platform logic
    if (movingPlat.x < 100 || movingPlat.x > 600)
    {
        movingPlat.velocityX = -movingPlat.velocityX;
    }
    
    // Enemy AI - bounce off walls
    for (var i = 0; i < len(enemies.members); i = i + 1)
    {
        var enemy = enemies.members[i];
        if (enemy.x < 0 || enemy.x > 800 - enemy.width)
        {
            enemy.velocityX = -enemy.velocityX;
        }
    }
    
    // ============================================
    // COLLISIONS - World handles everything!
    // ============================================
    
    // Players vs Platforms
    world.collide(players.members, platforms.members);
    
    // Enemies vs Platforms
    world.collide(enemies.members, platforms.members);
    
    // Enemies vs Enemies (bounce off each other)
    world.collide(enemies.members, enemies.members);
    
    // Players vs Enemies (with callback)
    world.overlap(players.members, enemies.members, onHitEnemy);
    
    // Players vs Collectibles (with callback)
    world.overlap(players.members, collectibles.members, onCollectCoin);
    
    // ============================================
    // RENDER
    // ============================================
    
    BeginDrawing();
    ClearBackground(BLACK);
    
    // Draw groups
    platforms.draw();
    collectibles.draw();
    enemies.draw();
    players.draw();
    emitter.draw();

    
    // UI
    DrawText("WASD/Arrows = Move | Space = Jump", 10, 10, 16, WHITE);
    DrawText(format("Score: {}", score), 10, 30, 20, Color(255, 255, 0, 255));
    DrawText("Jump on enemies! Collect coins!", 10, 55, 14, WHITE);
    DrawFPS(700, 10);
    
    EndDrawing();
}

CloseWindow();
 